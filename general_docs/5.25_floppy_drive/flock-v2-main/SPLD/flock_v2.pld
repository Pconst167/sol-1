GAL16V8    ;  Works with GAL16V8B and ATF16V8B
FLOCK_V2   ;  Flock Version 2

A1	A2	A3	A4	A5	A6	A7	M1	IORQ	GND
RD	WR	FDCCS	DACK	CCRWR	DORWR	RTCRD	RTCWR	RTCADDR	VCC

/CCRWR = /IORQ * M1 * /A7 * A6 * /A5 * /A4 * A3 * /A2 * /A1
/FDCCS = /IORQ * M1 * /A7 * A6 * /A5 * A4 * /A3 * /A2 * /A1
/DORWR = /IORQ * M1 * /A7 * A6 * /A5 * A4 * A3 * /A2 * /A1
/RTCWR = /WR * /IORQ * M1 * A7 * A6 * /A5 * /A4 * /A3 * /A2 * /A1 * /RTCADDR +
         /WR * /IORQ * M1 * /A7 * /A6 * /A5 * /A4 * A3 * A2 * /A1 * RTCADDR
/RTCRD = /RD * /IORQ * M1 * A7 * A6 * /A5 * /A4 * /A3 * /A2 * /A1 * /RTCADDR +
         /RD * /IORQ * M1 * /A7 * /A6 * /A5 * /A4 * A3 * A2 * /A1 * RTCADDR
/DACK = /RD * /IORQ * M1 * /A7 * A6 * /A5 * A4 * A3 * /A2 * /A1

DESCRIPTION

Address decode and control signals generation logic for Flock Version 2.0
Floppy Disk Controller (FDC) and Real Time Clock (RTC) module.

Flock uses the following I/O addresses:
- 0x48, write only: FDC Control Configuration Register (CCR)
- 0x48, read only: FDC Data Input Register (DIR)
- 0x50 - 0x51, read/write: FDC core (NEC uPD765/Intel 8272) - access to
  FDC main status register (0x50) and data register (0x51)
- 0x58, write only: FDC Data Output Register (DOR)
- 0x51, read only: Pulse TC to the FDC, indicating end of data transfer
- 0xC0, read/write: RTC command/data, Z80 mode, selected by RTCADDR low
- 0x0C, read/write: RTC command/data, Z180 mode, selected by RTCADDR high

Signals Description:
A1 - A7: Address inputs from the RCBUS

/M1, /IORQ, /RD, /WR: Control signals inputs from the RCBUS

/CCRWR: FDC CCR/DIR register chip select output. Connect to FDC the /LDCR input

/FDCCS: FDC core chip select output. Connect to the FDC /CS input

/DORWR: FDC DOR register chip select output. Connect to the FDC /LDOR input

/DACK: Pulse TC output. Connect to the FDC /DACK input. Connect FDC TC input
   to VCC. Internally in the FDC, TC is gated by /DACK. So pulsing /DACK in
   this configuration will pulse the TC to the FDC core

RTCADDR: RTC I/O address select input. Connect to the pull-down and a jumper
   to VCC. In this case, without the jumper the RTC address will be 0xC0
   (Z80 mode), and with the jumper it will be 0x0C (Z180 mode)

/RTCWR: RTC write enable output. Connect to the 74AHCT174 latch, that latches
   data and control signals from the data bus to the RTC

/RTCRD: RTC read enable output. Connect to the 74HCT125 three state buffer
   that outputs the RTC data to the data bus
